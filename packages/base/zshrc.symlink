# add extra bin directories to path
export PATH="/usr/local/bin:${PATH}"
export PATH="${HOME}/.local/bin:${PATH}"
export PATH="${HOME}/bin:${PATH}"
export PATH="./bin:${PATH}"

os="$(uname)"

# default world chars: *?_-.[]~=/&;!#$%^(){}<>
export WORDCHARS=""

# load installed zsh configs
for f in "${HOME}/.zsh_profile.d"/*.zsh; do
  source "$f"
done

# load secrets (shhhhhh)
[[ -f "${HOME}/.secrets" ]] && source "${HOME}/.secrets"

# load aliases
source "${HOME}/.zsh_aliases"

# add custom autocompletions to fpath
FPATH="${HOME}/.zsh_completions.d:${FPATH}"
# load autocompletions
autoload -Uz compinit && compinit
# allow case insensitive matching
zstyle ':completion:*' matcher-list 'm:{a-zA-Z}={A-Za-z}'
zstyle ':completion:*' menu select

# and display a nice banner
function mountains_header {
  # https://www.asciiart.eu/nature/mountains
  cat << "EOF"

          /\
         /**\
        /****\   /\
       /      \ /**\
      /  /\    /    \        /\    /\  /\      /\            /\/\/\  /\
     /  /  \  /      \      /  \/\/  \/  \  /\/  \/\  /\  /\/ / /  \/  \
    /  /    \/ /\     \    /    \ \  /    \/ /   /  \/  \/  \  /    \   \
   /  /      \/  \/\   \  /      \    /   /    \
__/__/_______/___/__\___\__________________________________________________
EOF
}

function header {
  mountains_header

  echo
  echo "$(whoami) : $(uname -n)"

  date="$(date +"%A, %B %d")"
  time="$(date +"%I:%M %p")"
  utc="$(date -u +"%H:%M UTC")"

  echo "${date} - ${time} - ${utc}"
  echo
}
header

# set emacs-style keybinds
bindkey -e

# override `cd` to run hooks in current directory (if applicable)
function cd {
  builtin cd "$@" || return 1
  [[ -f ./.cd_hook ]] && source ./.cd_hook || return 0
}
# finally, run any cd_hooks in the current dir
[[ -f ./.cd_hook ]] && source ./.cd_hook

